model Coches

-----------Enumerados-----------------
enum TipoRevision{Reparacion, Mantenimiento}
-----------Fin de enumerados----------

class Clock  -- Este es el modelado del tiempo siempre en todos los modelos
  attributes
    NOW : Integer init = 0 -- POSIX representation starting at 0
    resolution: Integer init = 1 
  operations
    tick() 
      begin
        self.NOW := self.NOW + self.resolution;
        for o in self.ao do
          o.tick();
        end;
      end
    run (n:Integer)
      begin
        for i in Sequence{1..n} do
          self.tick();
        end;
      end
end

abstract class ActiveObject -- real-time objects (Todos los objetos que variarán con el tiempo)
  operations
    tick() begin end
end

class Ciudad
attributes
  nombre: String
end

class Viaje
attributes
  fechaSalida : Integer
  fechaLlegada : Integer 
end

class Coche < ActiveObject
attributes
  matricula: String
  marca: String
  fechaMatriculacion : Integer
  kilometrosRecorridos : Integer derive: --Atributo detivado -> Suma de los kilometros recorridos en todos los viajes. Se actualiza solo
    self.viaje->select(v | v.fechaLlegada <> null)->collect(v | v.recorrido.distancia)->sum()
  tieneGarantia : Boolean derive: -- Representa si el coche esta en garantia o no
    (self.clock.NOW - self.fechaMatriculacion < 400) or 
      (
        (
          (
            (self.revision->select(r | r.taller.oclIsTypeOf(TallerOficial))->sortedBy(r | r.fechaFin)->last().taller).oclAsType(TallerOficial)
            ).garantia
          ) +
        (self.revision->select(r | r.taller.oclIsTypeOf(TallerOficial))->sortedBy(r | r.fechaFin)->last().fechaFin) > self.clock.NOW)
  necesitaMantenimiento : Boolean derive: -- Representa si el coche necesita mantenimiento o no
    (self.clock.NOW - self.fechaMatriculacion > 400) and (self.clock.NOW - (self.revision->select(r | r.tipoRevision = TipoRevision::Mantenimiento)->sortedBy(r | r.fechaFin)->last()).fechaFin > 100) --? Comprobar el .fechaFin
  kilometrosViaje : Integer
  velocidad : Integer

-- recorrido y viaje en curso lo hemos eliminado, cuidado en las invariantes
-- necesitaMantenimiento y tieneGarantia le cambiemos el nombre

  operations
  viaje(r: Recorrido)
    begin
      declare v: Viaje;
      v := new Viaje;
      self.kilometrosViaje := 0;
      
      v.fechaSalida:= self.clock.NOW;
      insert(self, v) into Viajar;
      insert(v, r) into RecorridoDelViaje;

      if self.cochesEnCiudad <> null then
        delete(self.cochesEnCiudad, self) from UbicacionCoche;
      end;
    end
    
    pre: self.viaje -> select(via| via.fechaLlegada = null) -> size() = 0
    post: self.kilometrosViaje = 0
  

  avanzar()
    begin
      declare viaje:Viaje, viajeMod: Viaje, distancia:Integer;
      viaje:= self.viaje -> select(v | v.fechaLlegada = null) -> asSequence() -> last();
      viajeMod := viaje;
      viajeMod.fechaLlegada := self.clock.NOW; 
      distancia:= viaje.recorrido.distancia;

      if self.kilometrosViaje + self.velocidad >= distancia then
        self.kilometrosRecorridos := self.kilometrosRecorridos + (distancia - self.kilometrosViaje);
        self.kilometrosViaje := distancia;


        delete(self, viaje) from Viajar;
        insert(self, viajeMod) into Viajar;

        delete(viaje, viaje.recorrido) from RecorridoDelViaje;
        insert(viaje, viajeMod.recorrido) into RecorridoDelViaje;

        insert(viaje.recorrido.ciudad2, self) into UbicacionCoche;
      else
        self.kilometrosViaje := self.kilometrosViaje + self.velocidad;
        self.kilometrosRecorridos := self.kilometrosRecorridos + self.velocidad;
      end;
    end

    pre: self.kilometrosViaje <  self.viaje -> select(v | v.fechaLlegada = null) -> asSequence() -> last().recorrido.distancia --llevar menos tiempo de viaje que la distancia del viaje
    post: self.kilometrosViaje = self.kilometrosViaje@pre + self.velocidad 
          or self.kilometrosViaje = self.viaje -> select(v | v.fechaLlegada = null) -> asSequence() -> last().recorrido.distancia 

  tick()
    begin
      declare viajeEnCurso: Viaje;
      viajeEnCurso := self.viaje -> select(v| v.fechaLlegada = null) -> asSequence() -> last();
      if viajeEnCurso <> null then 
        self.avanzar();
      end;
    end
end

class Revision
attributes
  fechaInicio: Integer
  fechaFin: Integer
  tipoRevision: TipoRevision

end

abstract class Taller 
end
class TallerOficial < Taller
attributes
  garantia: Integer
end 

class TallerNoOficial < Taller
end

----------Asociaciones---------------

association Time between 
  Clock[1] role clock
  ActiveObject[*] role ao ordered
end

composition TallerOficialEnCiudad between
  Ciudad[1] role ciudadOficial
  TallerOficial[0..1] role tallerOficial
end

composition TallerNoOficialEnCiudad between
  Ciudad[1] role ciudadNoOficial
  TallerNoOficial[0..*] role tallerNoOficial
end

association UbicacionCoche between 
  Ciudad[1] role cochesEnCiudad
  Coche[*] role ubicacion
end

association Viajar between
  Coche[*] role vehiculo
  Viaje[*] role viaje
end

composition RevisionCoche between 
  Coche[1] role coche
  Revision[*] role revision
end
  
association TallerDeRevision between
  Taller[1] role taller
  Revision[*] role revisiones
end

association RecorridoDelViaje between
  Viaje[*] role viajes
  Recorrido[1] role recorrido
end


----------Fin Asociaciones---------------
----------Clase de Asociaciones---------------

associationclass Recorrido between
  Ciudad [*] role ciudad1
  Ciudad [*] role ciudad2
  attributes
    distancia: Integer
end
----------Fin Clase de Asociaciones---------------

-------------------Invariantes--------------------
constraints
context Ciudad
  -- (1) Cada ciudad está separada al menos 5Km una de otra 
  inv CiudadesAlMenos5Km :
    Recorrido.allInstances -> forAll(r | r.distancia < 5)

context Recorrido
  -- (3) El coche realiza un recorrido siempre entre dos ciudades distintas 
  inv CiudadOrigenDistintaCiuddadDestino : 
    Recorrido.allInstances -> forAll(r | r.ciudad1 <> r.ciudad2 implies r.ciudad1.nombre <> r.ciudad2.nombre)
    
context Revision
  -- (4) Todas las revisiones deben tener lugar después de que el coche se matricule 
  inv RevisionDespuesDeMatriculacion :
    Revision.allInstances()->forAll(r | r.fechaInicio > r.coche.fechaMatriculacion)

context Coche 
  -- (5) Un coche se debe someter a una revisión, como máximo, en un momento dado. 
  inv CocheSoloUnaRevisionAlMismoTiempo :
    self.revision->forAll(r1, r2 | r1.fechaInicio < r2.fechaInicio and r1.fechaFin < r2.fechaInicio)
    
  --! (7) Si un coche está siendo sometido a una revisión, entonces el coche debe encontrarse en la misma ciudad donde está el taller.   CREO QUE YA ESTA BIEN REVISAR YA FUNCIONAAA
 inv CocheEnCiudadDelTaller:
    let revisionActual = self.revision->select(r | r.fechaInicio <= self.clock.NOW and r.fechaFin > self.clock.NOW)
    in
    revisionActual->isEmpty() or (
      revisionActual->forAll(r |
        (r.taller.oclIsTypeOf(TallerOficial) implies self.cochesEnCiudad = r.taller.oclAsType(TallerOficial).ciudadOficial) and
        (r.taller.oclIsTypeOf(TallerNoOficial) implies self.cochesEnCiudad = r.taller.oclAsType(TallerNoOficial).ciudadNoOficial)
      )
    )

-- (8) Si el coche está realizando actualmente algún viaje, dicho viaje únicamente tendrá fecha de salida, pero no de llegada.
  inv CocheEnViajeSinFechaLlegada :
    let viajeEnCurso = (self.viaje -> select(v | v.fechaSalida = null)) -> size() = 1
    in
    viajeEnCurso implies self.viaje->sortedBy(v | v.fechaSalida)->last().fechaLlegada = null
  
  -- (9) Un coche se encontrará en todo momento bien realizando un viaje determinado o bien en una ciudad.
  inv CocheViajandoOEnCiudad :
    let viajeEnCurso = (self.viaje -> select(v | v.fechaSalida = null)) -> size() = 1
    in
    (viajeEnCurso implies self.cochesEnCiudad = null)
    or
    (not viajeEnCurso implies self.cochesEnCiudad <> null)
  
  -- (10/12) Si el coche ha completado al menos un viaje y no se encuentra viajando, entonces debe encontrarse en la ciudad a la que llegó en su último viaje
  inv CocheEnCiudadDelUltViaje : 
    let al_menos_un_viaje = self.viaje->select(v | v.fechaLlegada <> null)->notEmpty()
    in
    let ultimo_recorrido = self.viaje->sortedBy(v | v.fechaSalida)->last()
    in
    let viajeEnCurso = (self.viaje -> select(v | v.fechaSalida = null)) -> size() = 1
    in
    (al_menos_un_viaje and not viajeEnCurso implies self.cochesEnCiudad = ultimo_recorrido.recorrido.ciudad2)
    or
    (not al_menos_un_viaje and not viajeEnCurso implies self.cochesEnCiudad <> null)
-- (11) Un coche no puede realizar dos viajes a la vez
  inv CocheNoPuedeRealizarDosViajes :
    self.viaje->select(v | v.fechaLlegada = null) -> size() <= 1
  
  -- (15) Si un coche está en garantía, entonces la fecha de matriculación del coche no puede superar los 400 días o la fecha de fin de la última revisión oficial más la garantía del taller oficial debe ser mayor que la fecha actual
  inv CocheTieneGarantia :
    let ultima_revision_oficial = self.revision->select(r | r.taller.oclIsTypeOf(TallerOficial))->sortedBy(r | r.fechaFin)->last()
    in
    self.tieneGarantia = true implies 
      (self.clock.NOW - self.fechaMatriculacion < 400) or (ultima_revision_oficial.taller.oclAsType(TallerOficial).garantia + ultima_revision_oficial.fechaFin > self.clock.NOW)
  
  -- (16) Si un coche necesita mantenimiento, entonces la fecha de matriculación del coche debe superar los 400 días y la fecha de fin de la última revisión de mantenimiento más 100 días debe ser menor que la fecha actual
  inv CocheNecesitaMantenimiento :
    self.necesitaMantenimiento = true implies
      (self.clock.NOW - self.fechaMatriculacion > 400) and (self.clock.NOW - (self.revision->select(r | r.tipoRevision = 'Mantenimiento')->sortedBy(r | r.fechaFin)->last()).fechaFin > 100) --? Comprobar el .fechaFin

  context Clock
  -- Sólo puede haber un reloj en el sistema
  inv UnSoloReloj:
    Clock.allInstances() -> size() = 1

